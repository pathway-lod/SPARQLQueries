PREFIX wdt:  <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?metabolite ?inchikey ?wd ?label
FROM <http://plantmetwiki.bioinformatics.nl/>
WHERE {
  {
    SELECT DISTINCT ?metabolite ?inchikey
    FROM <http://plantmetwiki.bioinformatics.nl/>
    WHERE {
      ?metabolite ?p ?o .
      FILTER(STRSTARTS(STR(?metabolite), "https://identifiers.org/inchikey/"))
      BIND(STRAFTER(STR(?metabolite), "https://identifiers.org/inchikey/") AS ?inchikey)
    }
    LIMIT 10
  }

  SERVICE <https://query.wikidata.org/sparql> {
    ?wd wdt:P235 ?inchikey .
    OPTIONAL { ?wd rdfs:label ?label . FILTER(LANG(?label) = "en") }
  }
}
ORDER BY ?metabolite