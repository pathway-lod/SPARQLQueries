PREFIX wp:      <http://vocabularies.wikipathways.org/wp#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX wdt:     <http://www.wikidata.org/prop/direct/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?pw ?title ?metabolite ?smilesDepict ?inchikey ?wd ?wdLabel
FROM <http://plantmetwiki.bioinformatics.nl/>
WHERE {
  {
    SELECT DISTINCT ?pw ?title ?metabolite ?inchikey
    FROM <http://plantmetwiki.bioinformatics.nl/>
    WHERE {
      ?metabolite a wp:Metabolite ;
                  dcterms:isPartOf ?pw .

      ?pw dc:title ?titleLit ;
          wp:organismName "Solanum tuberosum" .
      BIND(STR(?titleLit) AS ?title)

      FILTER(STRSTARTS(STR(?metabolite), "https://identifiers.org/inchikey/"))
      BIND(STRAFTER(STR(?metabolite), "https://identifiers.org/inchikey/") AS ?inchikey)
    }
    LIMIT 10
  }

  SERVICE <https://query.wikidata.org/sparql> {
    ?wd wdt:P235 ?inchikey .
    OPTIONAL { ?wd wdt:P233 ?smilesDepict }
    OPTIONAL { ?wd rdfs:label ?wdLabel . FILTER(LANG(?wdLabel) = "en") }
  }
}
ORDER BY ?title ?metabolite