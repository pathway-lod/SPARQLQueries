PREFIX wp:      <http://vocabularies.wikipathways.org/wp#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX wdt:     <http://www.wikidata.org/prop/direct/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?title (GROUP_CONCAT(DISTINCT ?species; separator=", ") as ?Species) ?inchikey ?wd ?wdLabel
FROM <http://plantmetwiki.bioinformatics.nl/>
WHERE {
      ?metabolite a wp:Metabolite ;
                  dcterms:isPartOf ?pw ;
                  dc:source "InChIKey";
                  dcterms:identifier ?inchikey .
  
      FILTER(?inchikey IN (
      "BGWGXPAPYGQALX-ARQDHWQXSA-L",
      "CZMRCDWAGMRECN-UGDNZRGBSA-N",
      "ASKKPQKSCFYPPP-FVLDFCIYSA-J")) ##metabolite list of interest

      ?pw dc:title ?title ;
           wp:organismName ?species .

  SERVICE <https://query.wikidata.org/sparql> {
   
    ?wd wdt:P235 ?inchikey .
    OPTIONAL { ?wd rdfs:label ?wdLabel . FILTER(LANG(?wdLabel) = "en") }
  }
} ORDER BY ?wd
